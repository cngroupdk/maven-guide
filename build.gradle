import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: 'war'

group = 'dk.cngroup.javacomp'
version = '1.0-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
}

sourceSets {
    e2eTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDirs += 'src/test/e2e'
        }
    }
}

configurations {
    e2eTestCompile.extendsFrom testCompile
    e2eTestRuntime.extendsFrom testRuntime
}

task e2eTest(type: Test) {
    group = 'Verification'
    testClassesDir = sourceSets.e2eTest.output.classesDir
    classpath = sourceSets.e2eTest.runtimeClasspath
    outputs.upToDateWhen { false }

    dependsOn war
}

def commandLineArgs(List args) {
    (Os.isFamily(Os.FAMILY_WINDOWS)) ? ['cmd', '/c'] + args : args
}

clean {
    delete "$webAppDirName/static/node_modules"
}

task npm(type: Exec) {
    group = 'Build'
    workingDir = "$webAppDirName/static"
    commandLine = commandLineArgs(['cmd', '/c', 'npm', 'install'])
}

war {
    dependsOn npm
}

dependencies {
    compile 'commons-lang:commons-lang:2.6'
    compile 'javax.ws.rs:javax.ws.rs-api:2.0'
    compile 'org.glassfish.jersey.containers:jersey-container-servlet-core:2.6'

    testCompile 'junit:junit:4.12'

    e2eTestCompile 'org.seleniumhq.selenium:selenium-java:2.53.0'
    e2eTestCompile 'io.github.bonigarcia:webdrivermanager:1.4.3'
}
