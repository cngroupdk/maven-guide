import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id "com.bmuschko.tomcat" version "2.2.4"
}

apply plugin: 'war'

group = 'dk.cngroup.javacomp'
version = '1.0-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
}

ext {
    tomcatVersion = '7.0.69'
}

sourceSets {
    e2eTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDirs += 'src/test/e2e'
        }
    }
}

configurations {
    e2eTestCompile.extendsFrom testCompile
    e2eTestRuntime.extendsFrom testRuntime
}

task npm(type: Exec) {
    group = 'Build'
    workingDir = "$webAppDirName/static"
    commandLine = commandLineArgs(['npm', 'install'])
}

task testNpm(type: Exec) {
    group = 'Build'
    workingDir = 'src/test/javascript'
    commandLine = commandLineArgs(['npm', 'install'])
}

task karma(type: Exec) {
    group = 'Verification'
    dependsOn testNpm

    workingDir = 'src/test/javascript/node_modules/.bin'
    commandLine = commandLineArgs(['karma', 'start', file('src/test/javascript/karma.conf.js'), '--single-run'])
}

task e2eTest(type: Test) {
    group = 'Verification'
    dependsOn npm, tomcatRun
    finalizedBy tomcatStop

    testClassesDir = sourceSets.e2eTest.output.classesDir
    classpath = sourceSets.e2eTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

def commandLineArgs(List args) {
    (Os.isFamily(Os.FAMILY_WINDOWS)) ? ['cmd', '/c'] + args : args
}

clean {
    delete "$webAppDirName/static/node_modules"
    delete "src/test/javascript/node_modules"
}

war {
    dependsOn npm
}

tomcat {
    contextPath = '/'
    daemon = true
}

dependencies {
    compile 'commons-lang:commons-lang:2.6'
    compile 'javax.ws.rs:javax.ws.rs-api:2.0'
    compile 'org.glassfish.jersey.containers:jersey-container-servlet-core:2.6'

    testCompile 'junit:junit:4.12'

    e2eTestCompile 'org.seleniumhq.selenium:selenium-java:2.53.0'
    e2eTestCompile 'io.github.bonigarcia:webdrivermanager:1.4.3'

    tomcat "org.apache.tomcat.embed:tomcat-embed-core:$tomcatVersion"
    tomcat "org.apache.tomcat.embed:tomcat-embed-logging-juli:$tomcatVersion"
    tomcat "org.apache.tomcat.embed:tomcat-embed-jasper:$tomcatVersion"
}
